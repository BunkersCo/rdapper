#!/usr/bin/perl
# Copyright (c) 2018 CentralNic Ltd. All rights reserved. This program is
# free software; you can redistribute it and/or modify it under the same
# terms as Perl itself.
use Getopt::Long;
use List::MoreUtils qw(any);
use Net::ASN;
use Net::DNS::Domain;
use Net::IP;
use Net::RDAP;
use Term::ANSIColor;
use vars qw($VERSION);
use strict;

$VERSION = 0.1;

sub warning {
	my ($fmt, @params) = @_;
	my $str = sprintf("Warning: $fmt", @params);
	print STDERR colored([qw(yellow)], $str)."\n";
}

sub error {
	my ($fmt, @params) = @_;
	my $str = sprintf("Error: $fmt", @params);
	print STDERR colored([qw(red)], $str)."\n";
	exit 1;
}

my $rdap = Net::RDAP->new;

my ($type, $object);
GetOptions(
	'type:s'	=> \$type,
	'object:s'	=> \$object,
);

$object = shift(@ARGV) if (!$object);

if (!$type) {
	if ($object =~ /^\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}$/)			{ $type = 'ip'		} # v4 address
	elsif ($object =~ /^\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}\/\d{1,2}$/)	{ $type = 'ip'		} # v4 range
	elsif ($object =~ /^[0-9a-f:]+$/i)					{ $type = 'ip'		} # v6 address
	elsif ($object =~ /^[0-9a-f:]+\/\d{1,3}$/i)				{ $type = 'ip'		} # v6 range
	elsif ($object =~ /^asn?\d+$/i)						{ $type = 'autnum'	} # ASN
	else									{ $type = 'domain'	} # domain
}

my $rdap = Net::RDAP->new;

my $response;
if ('ip' eq $type) {
	$response = $rdap->ip(Net::IP->new($object));

} elsif ('autnum' eq $type) {
	my $asn = $object;
	$asn =~ s/^asn?//ig;
	$response = $rdap->autnum(Net::ASN->new($asn));

} elsif ('domain' eq $type) {
	$response = $rdap->domain(Net::DNS::Domain->new($object));

} else {
	error("Unable to handle type '$type'");

}

if (!$response) {
	error("Unable to retrieve data");

} else {
	my $name = $response->name;
	printf("\nName: %s\n\n", uc('Net::DNS::Domain' eq ref($name) ? $name->name : $name)) if ($name);

	printf("Handle: %s\n\n", $response->handle);

	my @links = $response->links;
	if (scalar(@links) > 0) {
		print "Links:\n";
		foreach my $link (@links) {
			printf("\n  * %s (%s)\n", $link->href->as_string, $link->title || $link->rel || '-');
		}
		print "\n";
	}

	if ('ip' eq $type) {
		printf("Range: %s\n\n", $response->range->prefix);

		printf("Domain: %s\n\n", $response->domain->as_string);

	} elsif ('autnum' eq $type) {
		printf("Range: %u - %u\n\n", $response->start, $response->end) if ($response->start > 0 && $response->end > 0);
		printf("Type: %s\n\n", $response->type) if ($response->type);

	} elsif ('domain' eq $type) {
		my @ns = $response->nameservers;
		if (scalar(@ns) > 0) {
			print "Nameservers:\n\n";
			foreach my $ns (sort { lc($a->name->name) cmp lc($b->name->name) } @ns) {
				printf("  %s\n", uc($ns->name->name));
			}
			print "\n";
		}

		my @ds = $response->ds;
		if (scalar(@ds) > 0) {
			print "DNSSEC:\n\n";
			foreach my $ds ($response->ds) {
				printf("  %s. IN DS %u %u %u %s\n", uc($ds->name), $ds->keytag, $ds->algorithm, $ds->digtype, uc($ds->digest));
			}
			print "\n";
		}

		my @keys = $response->keys;
		if (scalar(@keys) > 0) {
			print "DNSSEC Keys:\n\n";
			foreach my $key (@keys) {
				printf("  %s. IN DNSKEY %u %u %u %s\n", uc($key->name), $key->flags, $key->protocol, $key->algorithm, uc($key->key));
			}
			print "\n";
		}

	} else {
		error("Unable to handle type '$type'");

	}

	print "Events:\n\n";
	foreach my $event ($response->events) {
		printf("  %s: %s\n", ucfirst($event->action), scalar($event->date));
	}
	print "\n";

	my @status = $response->status;
	if (scalar(@status) > 0) {
		print "Status:\n\n";
		foreach my $status (@status) {
			printf("  * %s\n", $status);
		}
		print "\n";
	}

	my @entities = $response->entities;
	my %entities;
	foreach my $entity (@entities) {
		eval {
			my $new = $rdap->fetch($entity) if (!$entity->vcard);
			$entity = $new if ($new);
		};
		foreach my $role ($entity->roles) {
			$entities{$role} = $entity;
		}
	}

	if (scalar(@entities) > 0) {
		print "Entities:\n\n";

		foreach my $entity (@entities) {
			my $card = $entity->vcard;
			if ($card) {
				my @roles = $entity->roles;
				if (scalar(@roles) > 0) {
					printf("  Entity %s (%s):\n\n", $entity->handle, join(', ', @roles));

				} else {
					printf("  Entity %s:\n\n", $entity->handle);
				}

				printf("    Name: %s\n", $card->full_name) if ($card->full_name);
				printf("    Organization: %s\n", $card->organization) if ($card->organization);

				foreach my $email (@{$card->email_addresses}) {
					if ($email->{'type'}) {
						printf("    Email: %s (%s)\n", $email->{'address'}, $email->{'type'});
						
					} else {
						printf("    Email: %s\n", $email->{'address'});

					}
				}

				foreach my $number (@{$card->phones}) {
					my @types = ('ARRAY' eq ref($number->{'type'}) ? @{$number->{'type'}} : ($number->{'type'}));
					my $type = ((any { lc($_) eq 'fax' } @types) ? 'Fax' : 'Phone');
					printf("    %s: %s\n", $type, $number->{'number'});
				}

				print "\n";
			}
		}
	}

	my @remarks = $response->remarks;
	if (scalar(@remarks) > 0) {
		print "Remarks:\n\n";
		foreach my $remark (@remarks) {
			my $indent = ' ' x 2;
			if ($remark->title) {
				printf("  %s:\n  %s\n\n", $remark->title, ('=' x (1 + length($remark->title))));
				$indent = ' ' x 4;
			}
			print $indent.join("\n$indent", $remark->description)."\n";
			foreach my $link ($remark->links) {
				printf("\n%s* %s (%s)\n", ($indent x 2), $link->href->as_string, ($link->title || $link->rel || '-'));
			}
			print "\n";
		}
	}

	my @notices = $response->notices;
	if (scalar(@notices) > 0) {
		print "Notices:\n\n";
		foreach my $notice (@notices) {
			my $indent = ' ' x 2;
			if ($notice->title) {
				printf("  %s:\n  %s\n\n", $notice->title, ('=' x (1 + length($notice->title))));
				$indent = ' ' x 4;
			}
			print $indent.join("\n$indent", $notice->description)."\n";
			foreach my $link ($notice->links) {
				printf("\n%s* %s (%s)\n", ($indent x 2), $link->href->as_string, ($link->title || $link->rel || '-'));
			}
			print "\n";
		}
	}
}

__END__

=pod

=head1 NAME

C<rdapper> - a simple console-based RDAP client.

=head1 SYNOPSIS

	rdapper OBJECT

C<rdapper> is a simple RDAP client. It uses L<Net::RDAP> to retrieve
data about internet resources (domain names, IP addresses, and
autonymous systems) and outputs the information in a human-readable
format. If you want to consume this data in your own program you
should use L<Net::RDAP> directly.

C<rdapper> was originally conceived as a full RDAP client (back
when the RDAP protocol was still in draft form) but is now just
a very thin front-end to L<Net::RDAP>, and its main purpose is to
allow testing of that library.

You can pass any internet resource as an argument; this may be a:

=over

=item * a "forward" domain name such as C<example.com>;

=item * a "reverse" domain name such as C<168.192.in-addr.arpa>;

=item * a IPv4 or IPv6 address or CIDR prefix, such as C<192.168.0.1> or C<2001:DB8::/32>;

=item * an Autonymous System Number such as C<AS65536>.

=back

=head1 DEPENDENCIES

C<rdapper> uses the following modules, some of which may already be
installed:

=over

=item * L<Getopt::Long>

=item * L<List::MoreUtils>

=item * L<Net::ASN>

=item * L<Net::DNS::Domain>

=item * L<Net::IP>

=item * L<Net::RDAP> (obviously)

=item * L<Term::ANSIColor>

=back

=head1 COPYRIGHT

Copyright 2018 CentralNic Ltd. All rights reserved.

=head1 LICENSE

Permission to use, copy, modify, and distribute this software and its
documentation for any purpose and without fee is hereby granted,
provided that the above copyright notice appear in all copies and that
both that copyright notice and this permission notice appear in
supporting documentation, and that the name of the author not be used
in advertising or publicity pertaining to distribution of the software
without specific prior written permission.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

=cut